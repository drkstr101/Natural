grammar org.agileware.natural.lang.Natural hidden(WS, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate model "http://www.agileware.org/natural/lang"

/**
 * 
 */
NaturalModel: {NaturalModel}
	BLANK_SPACE?
	document=Document?
;

/**
 * 
 */
Document: {Document}
	(meta=Meta BLANK_SPACE?)?
	'Document:' title=Text? NL
	(BLANK_SPACE? narrative=Narrative)?
	(BLANK_SPACE? sections+=Section)*
	BLANK_SPACE?
;

/**
 * 
 */	
Section: {Section}
	(meta=Meta BLANK_SPACE?)?
	'Section:' title=Text? NL
	(BLANK_SPACE? narrative=Narrative)?
;

/**
 * 
 */
Meta: {Meta}
	tags+=Tag
	  (BLANK_SPACE? tags+=Tag)*
	NL
;

/**
 * 
 */
Tag: {Tag}
	id=TAG_LITERAL
	(':' value=Text)?
;

terminal TAG_LITERAL: 
	'@' !('@' | ':'  | ' ' | '\t' | '\n' | '\r')+
;

/**
 * 
 */
Narrative: {Narrative}
   sections+=NarrativeSection
     (BLANK_SPACE sections+=NarrativeSection)*
;

/**
 * 
 */
NarrativeSection: Paragraph
	| DocString
	| Table
;

/**
 * 
 */
Paragraph: {Paragraph}
	value=MultilineText
;

/**
 * 
 */
DocString: {DocString}
	value=DOC_STRING_LITERAL
	NL
;

terminal DOC_STRING_LITERAL: 
	('"""' -> '"""')
	  | ("'''" -> "'''")
;

/**
 * 
 */
Table: {Table}
	rows+=TableRow+
;

/**
 * 
 */
TableRow: {TableRow}
	cols+=TableCol+ '|' NL
;

/**
 * 
 */
TableCol: {TableCol}
	value=TABLE_CELL
;

terminal TABLE_CELL: '|' !('|' | '\n' | '\r')*;


// --------------------------------------
// Text Model
// --------------------------------------

/**
 * 
 */
Text returns ecore::EString:
	TEXT_LITERAL+
;

TEXT_LITERAL: WORD_LITERAL
	| NUMBER_LITERAL 
	| STRING_LITERAL
//	| PUNCTUATION
	| TABLE_CELL 
	| ANY_OTHER
;

/**
 * 
 */
MultilineText returns ecore::EString:
	MULTILINE_TEXT_LITERAL+
;

MULTILINE_TEXT_LITERAL:
	(WORD_LITERAL | NUMBER_LITERAL | STRING_LITERAL | ANY_OTHER) TEXT_LITERAL* NL
;

terminal STRING_LITERAL returns ecore::EString:
	'"' ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | '\\') | !('\\' | '"' | '\r' | '\n'))* '"' |
	"'" ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | "'" | '\\') | !('\\' | "'" | '\r' | '\n'))* "'"
;

terminal NUMBER_LITERAL returns ecore::EBigInteger:
	'-'? DIGIT+ '.' DIGIT* EXPONENT_PART?
	| '-'? '.' DIGIT+ EXPONENT_PART?
	| '-'? DIGIT+ EXPONENT_PART
	| '-'? DECIMAL_NUMBER
	| '-'? HEX_NUMBER
	| '-'? OCTAL_NUMBER
;

terminal fragment DECIMAL_NUMBER:
	NON_ZERO_DIGIT DIGIT*
;

terminal fragment DIGIT: '0' | NON_ZERO_DIGIT;

terminal fragment NON_ZERO_DIGIT: '1'..'9';

terminal fragment HEX_NUMBER:
	'0' ('x' | 'X') ('0'..'9' | 'a'..'f' | 'A'..'F')+
;

terminal fragment OCTAL_NUMBER:
	'0' '0'..'7'*
;

terminal fragment EXPONENT_PART:
	('e' | 'E') ('+' | '-')? DIGIT+
;

terminal SL_COMMENT: '#' !('\n' | '\r')* NL;

terminal WORD_LITERAL: 
	!('@' | '|' | ' ' | '\t' | '\n' | '\r')
	!(' ' | '\t' | '\n' | '\r')*
;

//terminal WORD: 
//	(LETTER | DIGIT)
//	(LETTER | DIGIT | '_' | '-' | '.' )*
//;

//terminal fragment LETTER: LATIN_ALPHABET;
//terminal fragment LATIN_ALPHABET: ('\u0041'..'\u005A')
//	| ('\u0061'..'\u007A')
//;

BLANK_SPACE: NL+ WS*;
terminal NL: ('\r'? '\n') | EOF;

terminal WS: (' ' | '\t');

terminal ANY_OTHER: .;
