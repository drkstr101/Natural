grammar org.agileware.natural.cucumber.Cucumber with org.agileware.natural.lang.Natural

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate cucumber "http://www.agileware.org/natural/cucumber"

/**
 * 
 */
CucumberModel: {CucumberModel}
	BLANK_SPACE?
	document=Feature?
;

Feature: {Feature}
	(meta=Meta BLANK_SPACE?)?
	'Feature:' title=Text? NL
	(BLANK_SPACE? narrative=Narrative)?
	(BLANK_SPACE? scenarios+=AbstractScenario)*
	BLANK_SPACE?
;

/**
 * TODO default grammar element triggers backtracking
 */
@Override
Narrative: {Narrative}
   sections+=Paragraph
     (BLANK_SPACE sections+=Paragraph)*
;

/**
 * Base class for Scenario implementations. 
 * A Scenario is any type with the following structure:
 * 
 * tags+=Tag*
 * 'Scenario:'
 * title=Title? EOL+
 * narrative=Narrative?
 * steps+=Step+;
 */
AbstractScenario: Scenario
	| ScenarioOutline
	| Background
;

Background: {Background}
	(meta=Meta BLANK_SPACE?)?
	'Background:' title=Text? NL
	(BLANK_SPACE? narrative=Narrative)?
	(BLANK_SPACE? steps+=Step)*
;

Scenario: {Scenario}
	(meta=Meta BLANK_SPACE?)?
	'Scenario:' title=Text? NL
	(BLANK_SPACE? narrative=Narrative)?
	(BLANK_SPACE? steps+=Step)*
;

ScenarioOutline: {ScenarioOutline}
	(meta=Meta BLANK_SPACE?)?
	'Scenario' 'Outline:' title=Text? NL
	(BLANK_SPACE? narrative=Narrative)?
	(BLANK_SPACE? steps+=Step)*
	(BLANK_SPACE? examples+=Example)*
;


Example: {Example}
	(meta=Meta BLANK_SPACE?)?
	'Examples:' title=Text? NL
	(BLANK_SPACE? narrative=Narrative)?
	table=Table
;

Step: {Step}
	keyword=STEP_KEYWORD
	description=StepDescription NL
	((BLANK_SPACE? table=Table) | (BLANK_SPACE? text=DocString))?
;


/**
 * 
 */
StepDescription returns ecore::EString:
	STEP_DESCRIPTION_LITERAL+
;

STEP_DESCRIPTION_LITERAL: PLACEHOLDER
	| TEXT_LITERAL
;

DocString: {DocString}
	('"""' NL -> text=MultilineText '"""')
	// TODO alternate quote support in formatter
	// | ("'''" NL -> text=Text? "'''")
	NL
;


terminal STEP_KEYWORD: ('Given' | 'When' | 'Then' | 'And' | 'But') (' ' | '\t')+;

terminal PLACEHOLDER: '<' !('>' | ' ' | '\t' | '\n' | '\r')+ '>';
	