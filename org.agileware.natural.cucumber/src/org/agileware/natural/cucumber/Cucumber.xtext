grammar org.agileware.natural.cucumber.Cucumber with org.agileware.natural.lang.Natural

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate cucumber "http://www.agileware.org/natural/cucumber"

/**
 * 
 */
CucumberModel: {CucumberModel}
	BLANK_SPACE?
	document=Feature?
;

/**
 * 
 */
Feature: {Feature}
	(meta=Meta BLANK_SPACE?)?
	'Feature:' title=Title? NL
	(BLANK_SPACE? narrative=Narrative)?
	(BLANK_SPACE? scenarios+=AbstractScenario)*
	BLANK_SPACE?
;

/**
 * Base class for Scenario implementations. 
 * An AbstractScenario is any type with the following structure:
 * 
 * meta=Meta?
 * 'Keyword:' title=Title? NL
 * narrative=Narrative?
 * steps+=Step*
 */
AbstractScenario: Scenario
	| ScenarioOutline
	| Background
;

/**
 * 
 */
Background: {Background}
	(meta=Meta BLANK_SPACE?)?
	'Background:' title=Title? NL
	(BLANK_SPACE? narrative=Narrative)?
	(BLANK_SPACE? steps+=Step)*
;

/**
 * 
 */
Scenario: {Scenario}
	(meta=Meta BLANK_SPACE?)?
	'Scenario:' title=Title? NL
	(BLANK_SPACE? narrative=Narrative)?
	(BLANK_SPACE? steps+=Step)*
;

/**
 * 
 */
ScenarioOutline: {ScenarioOutline}
	(meta=Meta BLANK_SPACE?)?
	'Scenario' 'Outline:' title=Title? NL
	(BLANK_SPACE? narrative=Narrative)?
	(BLANK_SPACE? steps+=Step)*
	(BLANK_SPACE? examples+=Example)*
;

/**
 * 
 */
Step: {Step}
	keyword=STEP_KEYWORD
	description=STEP_DESCRIPTION NL
	((BLANK_SPACE? table=Table) | (BLANK_SPACE? text=DocString))?
;

STEP_DESCRIPTION:
	(WORD | NUMBER | STRING | PLACEHOLDER | STEP_KEYWORD | TAGNAME)+
;

terminal STEP_KEYWORD: ('Given' | 'When' | 'Then' | 'And' | 'But' | '*');

terminal PLACEHOLDER: '<' !('>' | ' ' | '\t' | '\n' | '\r')+ '>';

/**
 * 
 */
Example: {Example}
	(meta=Meta BLANK_SPACE?)?
	'Examples:' title=Title? NL
	(BLANK_SPACE? narrative=Narrative)?
	(BLANK_SPACE? table=Table)
;


@Override 
TITLE_LITERAL:
	(WORD | NUMBER | STRING | PLACEHOLDER)
	(WORD | NUMBER | STRING | PLACEHOLDER | STEP_KEYWORD | TAGNAME)*
;

@Override
NARRATIVE_LITERAL:
	(
		(WORD | NUMBER | STRING | PLACEHOLDER)
		(WORD | NUMBER | STRING | TAGNAME | TABLE_CELL | PLACEHOLDER | STEP_KEYWORD)*
		NL
	)+
	(
		NL+
		(WORD | NUMBER | STRING | PLACEHOLDER)
		(WORD | NUMBER | STRING | TAGNAME | TABLE_CELL | PLACEHOLDER | STEP_KEYWORD)*
		NL
	)*
;

// Note: We must override so that terminal order is preserved
@Override
terminal NUMBER returns ecore::EDouble:
	'-'?
	DIGIT+
	('.' DIGIT+)?
;

@Override 
terminal WORD: 
	!('@' | '|' | ' ' | '\t' | '\n' | '\r')
	!(' ' | '\t' | '\n' | '\r')*
;

